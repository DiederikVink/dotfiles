#+TITLE: GNU Emacs Configuration
#+DATE: <2017-08-11 Fri>
#+AUTHOR: Yann Herklotz

* Introduction
This is my GNU Emacs Configuration that is mostly focused on C++ development,
but also has support for Python, F#, Haskell and Clojure.

#+BEGIN_SRC emacs-lisp
  (setq user-full-name "Yann Herklotz")
  (setq user-mail-address "ymherklotz@gmail.com")
#+END_SRC

#+RESULTS:
: ymherklotz@gmail.com

* Setup

Set path so that it picks up some executables that I use

#+BEGIN_SRC emacs-lisp
  (setenv "PATH"
    (concat
     (expand-file-name "~/.local/bin") ":"
     (expand-file-name "~/.yarn/bin") ":"
     (getenv "PATH")))
#+END_SRC

#+RESULTS:
: /home/yannherklotz/.local/bin:/home/yannherklotz/.yarn/bin:/home/yannherklotz/.local/bin:/home/yannherklotz/.yarn/bin:/usr/local/bin:/usr/bin:/bin:/usr/local/sbin:/opt/clojurescript/bin:/usr/lib/jvm/default/bin:/usr/bin/site_perl:/usr/bin/vendor_perl:/usr/bin/core_perl

** Repositories
Defining all the package repositories that are going to be used.

- ~gnu~ :: The default package repository for emacs
- ~melpa~ :: Contains a lot of additional packages for emacs that are made by
             the community.
- ~melpa-stable~ :: The stable melpa repository that only contains that full
                    versions for packages. This repository will be used for
                    packages that maybe get updated often, so that they do not
                    break the config.
- ~org~ :: org package repository that contains many packages to extend org-mode.

#+BEGIN_SRC emacs-lisp
  (require 'package)

  (defvar gnu          '("gnu"          . "https://elpa.gnu.org/packages/"))
  (defvar melpa        '("melpa"        . "https://melpa.org/packages/"))
  (defvar melpa-stable '("melpa-stable" . "https://stable.melpa.org/packages/"))
  (defvar org-elpa     '("org"          . "https://orgmode.org/elpa/"))
#+END_SRC

These packages are then added to the list of package archives.

#+BEGIN_SRC emacs-lisp
  (setq package-archives nil)
  (add-to-list 'package-archives melpa-stable t)
  (add-to-list 'package-archives melpa t)
  (add-to-list 'package-archives gnu t)
  (add-to-list 'package-archives org-elpa t)
#+END_SRC

Initialise the packages and if the directories don't exist, create them.

#+BEGIN_SRC emacs-lisp
  (setq package-enable-at-startup nil)
  (package-initialize)
#+END_SRC

Use ~use-package~ to manage other packages, and improve load times.

#+BEGIN_SRC emacs-lisp
  (require 'use-package)

  (setq use-package-always-ensure t)
#+END_SRC

** GC Threshold
Threshold for faster startup.

#+BEGIN_SRC emacs-lisp
  (setq gc-cons-threshold 500000000)
#+END_SRC

** General Configuration

*** Editor settings

Editor specific options such as adding line numbers.

Disable UI that starts when starting emacs and also set the y or n
instead of yes or no. Also stop the start up message from popping up
and enter the scratch buffer instead.

#+BEGIN_SRC emacs-lisp
  (setq inhibit-startup-message t
        confirm-nonexistent-file-or-buffer nil)
  (tool-bar-mode 0)
  (menu-bar-mode 0)
  (fset 'yes-or-no-p 'y-or-n-p)
#+END_SRC

#+RESULTS:
: y-or-n-p

Splitting of windows made more deterministic

#+BEGIN_SRC emacs-lisp
  (setq display-buffer-reuse-frames t)
  (setq pop-up-windows nil)

  ;; fix window splitting behavior when possible
  (setq display-buffer-alist
        '(("\\*cider-repl .*"
           (display-buffer-reuse-window display-buffer-in-side-window)
           (reusable-frames . visible)
           (side . bottom)
           (window-height . 0.2))
          ("\\*compilation\\*"
           (display-buffer-reuse-window display-buffer-same-window))
          ;; default
          (".*"
           (display-buffer-same-window))))
#+END_SRC

#+RESULTS:
| \*cider-repl .* | (display-buffer-reuse-window display-buffer-in-side-window) | (reusable-frames . visible) | (side . bottom) | (window-height . 0.2) |
| \*compilation\* | (display-buffer-reuse-window display-buffer-same-window)    |                             |                 |                       |
| .*              | (display-buffer-same-window)                                |                             |                 |                       |

*** Custom modeline

Editing the modeline. ~%c~ might be a bit slow though, so that could
be removed if that is ever a problem.

#+BEGIN_SRC emacs-lisp
  (defun -custom-modeline-github-vc ()
    (let ((branch (mapconcat 'concat (cdr (split-string vc-mode "[:-]")) "-")))
      (concat
       (propertize (format " %s" (all-the-icons-octicon "git-branch"))
                   'face `(:height 1 :family ,(all-the-icons-octicon-family))
                   'display '(raise 0))
       (propertize (format " %s" branch))
       (propertize "  "))))

  (defun -custom-modeline-svn-vc ()
    (let ((revision (cadr (split-string vc-mode "-"))))
      (concat
       (propertize (format " %s" (all-the-icons-faicon "cloud"))
                   'face `(:height 1)
                   'display '(raise 0))
       (propertize (format " %s" revision) 'face `(:height 0.9)))))

  (defvar y/mode-line-vc
    '(:propertize
      (:eval (when vc-mode
               (cond
                ((string-match "Git[:-]" vc-mode) (-custom-modeline-github-vc))
                ((string-match "SVN-" vc-mode) (-custom-modeline-svn-vc))
                (t (format "%s" vc-mode)))))
      face mode-line)
    "Formats the current directory.")

  (define-minor-mode minor-mode-blackout-mode
    "Hides minor modes from the mode line."
    t)

  (catch 'done
    (mapc (lambda (x)
            (when (and (consp x)
                       (equal (cadr x) '("" minor-mode-alist)))
              (let ((original (copy-sequence x)))
                (setcar x 'minor-mode-blackout-mode)
                (setcdr x (list "" original)))
              (throw 'done t)))
          mode-line-modes))

  (setq-default mode-line-format
                (list
                 " " mode-line-modified
                 " %[" mode-line-buffer-identification "%] %6l:%3c %6 "
                 mode-line-modes
                 " %6 "
                 y/mode-line-vc
                 mode-line-end-spaces))
#+END_SRC

#+RESULTS:
|   | (%1* %1+) | %[ | (%12b) | %] %6l:%3c %6 | (%[ ( (:propertize ( mode-name) help-echo Major mode |

Move the backup files into the temporaty directory so that they are out of the
way.

#+BEGIN_SRC emacs-lisp
  (setq backup-directory-alist
        `((".*" . ,temporary-file-directory)))
  (setq auto-save-file-name-transforms
        `((".*" ,temporary-file-directory t)))
#+END_SRC

#+RESULTS:
| .* | /tmp/ | t |

Make emacs follow symlinks every time, this means that it will open the actual
file and go to where the file is actually stored instead of editing it through
the symlink. This enables the use of git and other version control when editing
the file.
#+BEGIN_SRC emacs-lisp
  (setq vc-follow-symlinks t)
#+END_SRC

#+RESULTS:
: t

Make it easier to refresh the buffer by setting it to ~<f5>~.
#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "<f5>") 'revert-buffer)
#+END_SRC

#+RESULTS:
: revert-buffer

This stops paren mode with interfering with the modeline.
#+BEGIN_SRC emacs-lisp
  (show-paren-mode 'expression)
#+END_SRC

#+RESULTS:
: t

Revert the buffer automatically when a file changes on disc. This is useful when
monitoring a file such as a log file. It will also do this silently.
#+BEGIN_SRC emacs-lisp
  (global-auto-revert-mode 1)
  (setq auto-revert-verbose nil)
#+END_SRC

#+RESULTS:

Disable tabs, I want to move towards only using spaces everywhere as that is my
preferred style. This is just personal preference though.
#+BEGIN_SRC emacs-lisp
  (setq-default indent-tabs-mode nil)
  (setq-default tab-width 4)
  (defvaralias 'python-indent-offset 'tab-width)
  (defvaralias 'c-basic-offset 'tab-width)
#+END_SRC

#+RESULTS:
: tab-width

Set the line number display very high so that it is always shown in the modeline.
#+BEGIN_SRC emacs-lisp
  (setq line-number-display-limit 2000000)
#+END_SRC

#+RESULTS:
: 2000000

Set the undo correctly
#+BEGIN_SRC emacs-lisp
  (define-key global-map (kbd "C-\\") 'undo-only)
#+END_SRC

#+RESULTS:
: undo-only

Setting up my keybindings

#+BEGIN_SRC emacs-lisp
  (defun y/swap-windows ()
    "Swaps two windows and leaves the cursor in the original one"
    (interactive)
    (ace-swap-window)
    (aw-flip-window))

  (defun y/exit-emacs-client ()
    "consistent exit emacsclient. If not in emacs client, echo a
    message in minibuffer, don't exit emacs. If in server mode and
    editing file, do C-x # server-edit else do C-x 5 0
    delete-frame"
    (interactive)
    (if server-buffer-clients
        (server-edit)
      (delete-frame)))

  (defun y/beautify-json ()
    (interactive)
    (let ((b (if mark-active (min (point) (mark)) (point-min)))
          (e (if mark-active (max (point) (mark)) (point-max))))
      (shell-command-on-region b e
                               "python -m json.tool" (current-buffer) t)))

  (define-prefix-command 'y-map)
  (global-set-key (kbd "C-c y") 'y-map)

  (global-set-key (kbd "C-c q") 'y/exit-emacs-client)

  (define-key y-map (kbd "s") 'y/swap-windows)
  (define-key y-map (kbd "j") 'y/beautify-json)
#+END_SRC

#+RESULTS:
: y/beautify-json

Set the font to Hack, which is an opensource monospace font designed for
programming and looking at source code.

#+BEGIN_SRC emacs-lisp
  (set-default-font "Hack-11")
  (setq default-frame-alist '((font . "Hack-11")))
#+END_SRC

#+RESULTS:
: ((font . Hack-11))

#+BEGIN_SRC emacs-lisp
  (use-package eshell
    :ensure nil
    :bind (("C-c e" . eshell)))
#+END_SRC

#+RESULTS:
: eshell

* Social
** Mail
~mu4e~ is automatically in the load path when installed through a package
manager.

For archlinux, the command to install mu4e is:

#+BEGIN_SRC shell
  pacman -S mu
#+END_SRC

which comes with mu.

Set the email client to be mu4e in emacs, and set the correct mail directory. As
I am downloading all the mailboxes, there will be duplicates, which can be
ignored in searches by setting ~mu4e-headers-skip-duplicates~.

Also delete messages when they are sent, and don't copy them over to the sent
directory, as Gmail will do that for us.

To download the mail using imap, I use ~mbsync~, which downloads all mail with
the ~-a~ flag.

Finally, remove buffers when an email has been sent.

#+BEGIN_SRC emacs-lisp
  (use-package mu4e
    :ensure nil
    :commands mu4e
    :config 
    (add-hook 'message-mode-hook 'turn-on-orgtbl)
    (add-hook 'message-mode-hook 'turn-on-orgstruct++)

    (setq mail-user-agent 'mu4e-user-agent
          mu4e-maildir (expand-file-name "~/.mail")
          mu4e-headers-skip-duplicates t
          mu4e-sent-messages-behavior 'sent
          mu4e-get-mail-command "mbsync -a"
          message-kill-buffer-on-exit t
          mu4e-completing-read-function 'completing-read
          mu4e-context-policy 'pick-first
          mu4e-confirm-quit nil
          mu4e-html2text-command "pandoc -f html -t plain -"
          mu4e-change-filenames-when-moving t)

    ;; Try to show images
    (setq mu4e-view-show-images t
          mu4e-show-images t
          mu4e-view-image-max-width 800)

    ;; Mail directory shortcuts
    (setq mu4e-maildir-shortcuts
          '(("/gmail/Inbox" . ?g)
            ("/gmail/MyArchive" . ?r)
            ("/imperial/Inbox" . ?i)
            ("/imperial/MyArchive" . ?a)))

    (setq mu4e-contexts
          `( ,(make-mu4e-context
               :name "Gmail"
               :match-func (lambda (msg)
                             (when msg
                               (string-match-p "^/gmail" (mu4e-message-field msg :maildir))))
               :vars '((user-mail-address            . "ymherklotz@gmail.com")
                       (user-full-name               . "Yann Herklotz")
                       (mu4e-sent-folder             . "/gmail/[Gmail]/Sent Mail")
                       (mu4e-drafts-folder           . "/gmail/[Gmail]/Drafts")
                       (mu4e-trash-folder            . "/gmail/[Gmail]/Trash")
                       (mu4e-refile-folder           . "/gmail/MyArchive")
                       (smtpmail-smt-user            . "ymherklotz@gmail.com")
                       (smtpmail-local-domain        . "gmail.com")
                       (smtpmail-default-smtp-server . "smtp.gmail.com")
                       (smtpmail-smtp-server         . "smtp.gmail.com")
                       (smtpmail-smtp-service        . 587)))
             ,(make-mu4e-context
               :name "Imperial"
               :match-func (lambda (msg)
                             (when msg
                               (string-match-p "^/imperial" (mu4e-message-field msg :maildir))))
               :vars '((user-mail-address            . "yann.herklotz15@imperial.ac.uk")
                       (user-full-name               . "Yann Herklotz")
                       (mu4e-sent-folder             . "/imperial/Sent Items")
                       (mu4e-drafts-folder           . "/imperial/Drafts")
                       (mu4e-trash-folder            . "/imperial/Deleted Items")
                       (mu4e-refile-folder           . "/imperial/MyArchive")
                       (smtpmail-smt-user            . "ymh15@ic.ac.uk")
                       (smtpmail-local-domain        . "cc.ic.ac.uk")
                       (smtpmail-default-smtp-server . "smtp.cc.ic.ac.uk")
                       (smtpmail-smtp-server         . "smtp.cc.ic.ac.uk")
                       (smtpmail-smtp-service        . 587))))))
#+END_SRC

#+RESULTS:
: t

Setting up ~smtp~ to send messages using gmail.

#+BEGIN_SRC emacs-lisp
  (use-package smtpmail
    :ensure nil
    :config
    (setq message-send-mail-function 'smtpmail-send-it
          starttls-use-gnutls t))
#+END_SRC

#+RESULTS:
: t

To enable storing links in mu4e

#+BEGIN_SRC emacs-lisp
  (use-package org-mu4e
    :ensure nil)
#+END_SRC

#+RESULTS:

** Elfeed

#+BEGIN_SRC emacs-lisp
  (use-package elfeed-org
    :config
    (elfeed-org)
    (setq rmh-elfeed-org-files (list (expand-file-name "~/Dropbox/org/elfeed.org"))))

  (use-package elfeed
    :bind (:map elfeed-search-mode-map
                ("A" . y/elfeed-show-all)
                ("E" . y/elfeed-show-emacs)
                ("D" . y/elfeed-show-daily)
                ("q" . y/elfeed-save-db-and-bury)))
#+END_SRC

#+RESULTS:
: y/elfeed-save-db-and-bury

Define utility functions to make the reader work.

#+BEGIN_SRC emacs-lisp
  (defun y/elfeed-show-all ()
    (interactive)
    (bookmark-maybe-load-default-file)
    (bookmark-jump "elfeed-all"))

  (defun y/elfeed-show-emacs ()
    (interactive)
    (bookmark-maybe-load-default-file)
    (bookmark-jump "elfeed-emacs"))

  (defun y/elfeed-show-daily ()
    (interactive)
    (bookmark-maybe-load-default-file)
    (bookmark-jump "elfeed-daily"))

  ;;functions to support syncing .elfeed between machines
  ;;makes sure elfeed reads index from disk before launching
  (defun y/elfeed-load-db-and-open ()
    "Wrapper to load the elfeed db from disk before opening"
    (interactive)
    (elfeed-db-load)
    (elfeed)
    (elfeed-search-update--force))

  ;;write to disk when quiting
  (defun y/elfeed-save-db-and-bury ()
    "Wrapper to save the elfeed db to disk before burying buffer"
    (interactive)
    (elfeed-db-save)
    (quit-window))
#+END_SRC

#+RESULTS:
: y/elfeed-save-db-and-bury

* Utility
** Navigation

Set navigation commands in all the buffers
#+BEGIN_SRC emacs-lisp
  (defun prev-window ()
    (interactive)
    (other-window -1))

  (global-set-key (kbd "C-.") #'other-window)
  (global-set-key (kbd "C-,") #'prev-window)
#+END_SRC

#+RESULTS:
: prev-window

*** Ivy
#+BEGIN_SRC emacs-lisp
  (use-package ivy
    :bind
    (("C-s"     . swiper)
     ("M-x"     . counsel-M-x)
     ("C-x C-f" . counsel-find-file)
     ("C-c g"   . counsel-git)
     ("C-c j"   . counsel-git-grep)
     ("C-c k"   . counsel-ag)
     ("C-c C-r" . ivy-resume)
     ("C-x b"   . counsel-ibuffer)
     ("C-c y u" . counsel-unicode-char))
    :config
    (setq ivy-use-virtual-buffers t)
    (setq ivy-count-format "(%d/%d) ")
    (setq ivy-re-builders-alist
          '((swiper . ivy--regex-plus)
            (t      . ivy--regex-fuzzy))))
#+END_SRC

#+RESULTS:
: counsel-unicode-char

** Visual
*** All the icons
#+BEGIN_SRC emacs-lisp
  (use-package all-the-icons)
#+END_SRC

#+RESULTS:

** Editing
*** Hungry Delete
#+BEGIN_SRC emacs-lisp
  (use-package hungry-delete
    :config
    (global-hungry-delete-mode))
#+END_SRC

#+RESULTS:
: t

*** SmartParens
#+BEGIN_SRC emacs-lisp
  (use-package smartparens
    :bind (("M-["              . sp-backward-unwrap-sexp)
           ("M-]"              . sp-unwrap-sexp)
           ("C-M-f"            . sp-forward-sexp)
           ("C-M-b"            . sp-backward-sexp)
           ("C-M-d"            . sp-down-sexp)
           ("C-M-a"            . sp-backward-down-sexp)
           ("C-M-e"            . sp-up-sexp)
           ("C-M-u"            . sp-backward-up-sexp)
           ("C-M-t"            . sp-transpose-sexp)
           ("C-M-n"            . sp-next-sexp)
           ("C-M-p"            . sp-previous-sexp)
           ("C-M-k"            . sp-kill-sexp)
           ("C-M-w"            . sp-copy-sexp)
           ("C-)"              . sp-forward-slurp-sexp)
           ("C-}"              . sp-forward-barf-sexp)
           ("C-("              . sp-backward-slurp-sexp)
           ("C-{"              . sp-backward-barf-sexp)
           ("M-D"              . sp-splice-sexp)
           ("C-]"              . sp-select-next-thing-exchange)
           ("C-<left_bracket>" . sp-select-previous-thing)
           ("C-M-]"            . sp-select-next-thing)
           ("M-F"              . sp-forward-symbol)
           ("M-B"              . sp-backward-symbol)
           ("M-S"              . sp-split-sexp))
    :hook ((minibuffer-setup)  . turn-on-smartparens-strict-mode)
    :config
    (require 'smartparens-config)
    (show-smartparens-global-mode +1)
    (smartparens-global-mode 1)

    (sp-with-modes '(c-mode c++-mode)
      (sp-local-pair "{" nil :post-handlers '(("||\n[i]" "RET")))
      (sp-local-pair "/*" "*/" :post-handlers '((" | " "SPC")
                                                ("* ||\n[i]" "RET")))))
#+END_SRC

*** Whitespace
#+BEGIN_SRC emacs-lisp
  (use-package whitespace
    :bind (("C-x w" . whitespace-mode)))
#+END_SRC

*** IEdit
#+BEGIN_SRC emacs-lisp
  (use-package iedit
    :bind (("C-;" . iedit-mode)))
#+END_SRC

*** Expand Region

Expand region is very useful to select words and structures quickly by
incrementally selecting more and more of the text.

#+BEGIN_SRC emacs-lisp
  (use-package expand-region
    :bind ("M-m" . er/expand-region))
#+END_SRC

*** Dired

#+BEGIN_SRC emacs-lisp
  (add-hook 'dired-load-hook
            (function (lambda () (load "dired-x"))))

  (setq dired-dwim-target t)
#+END_SRC

* Writing
** Spellcheck in emacs
#+BEGIN_SRC emacs-lisp
  (defun spell-buffer-german ()
    (interactive)
    (ispell-change-dictionary "de_DE")
    (flyspell-buffer))

  (defun spell-buffer-english ()
    (interactive)
    (ispell-change-dictionary "en_UK")
    (flyspell-buffer))

  (use-package ispell
    :config
    (when (executable-find "hunspell")
      (setq-default ispell-program-name "hunspell")
      (setq ispell-really-hunspell t))

    ;; (setq ispell-program-name "aspell"
    ;;       ispell-extra-args '("--sug-mode=ultra"))
    :bind (("C-c N" . spell-buffer-german)
           ("C-c n" . spell-buffer-english)))

  (use-package flyspell
    :ensure nil
    :hook ((text-mode) . flyspell-mode)
    :config
    (define-key flyspell-mode-map (kbd "C-.") nil)
    (define-key flyspell-mode-map (kbd "C-,") nil))
#+END_SRC

** Latex
#+BEGIN_SRC emacs-lisp
  (use-package latex
    :ensure auctex
    :config
    (require 'tex-site)
    ;; to use pdfview with auctex
    (setq TeX-view-program-selection '((output-pdf "PDF Tools"))
          TeX-view-program-list '(("PDF Tools" TeX-pdf-tools-sync-view))
          TeX-source-correlate-start-server t) ;; not sure if last line is neccessary
    ;; to have the buffer refresh after compilation
    (add-hook 'TeX-after-compilation-finished-functions
              #'TeX-revert-document-buffer)
    (setq TeX-auto-save t)
    (setq TeX-parse-self t)
    (setq TeX-save-query nil)
    (setq-default TeX-master nil)
    (setq TeX-PDF-mode t)
    (add-hook 'LaTeX-mode-hook 'flyspell-mode)
    (add-hook 'LaTeX-mode-hook 'flyspell-buffer)
    (defun turn-on-outline-minor-mode ()
      (outline-minor-mode 1))

    (add-hook 'LaTeX-mode-hook 'turn-on-outline-minor-mode)
    (setq outline-minor-mode-prefix "\C-c \C-o")
    (autoload 'reftex-mode "reftex" "RefTeX Minor Mode" t)
    (autoload 'turn-on-reftex "reftex" "RefTeX Minor Mode" nil)
    (autoload 'reftex-citation "reftex-cite" "Make citation" nil)
    (autoload 'reftex-index-phrase-mode "reftex-index" "Phrase Mode" t)
    (add-hook 'latex-mode-hook 'turn-on-reftex) ; with Emacs latex mode

    (require 'ox-latex)
    (add-to-list 'org-latex-packages-alist '("" "minted"))
    (setq org-latex-listings 'minted))
#+END_SRC

** Markdown
Markdown is the standard for writing documentation. This snippet loads GFM
(Github Flavoured Markdown) style.

#+BEGIN_SRC emacs-lisp
  (use-package markdown-mode
    :commands (markdown-mode gfm-mode)
    :mode (("README\\.md\\'" . gfm-mode)
           ("\\.md\\'"       . markdown-mode)
           ("\\.markdown\\'" . markdown-mode))
    :init (setq markdown-command "multimarkdown"))
#+END_SRC

** Org
Agenda setup for org mode, pointing to the write files.

#+BEGIN_SRC emacs-lisp
  (setq org-log-into-drawer t)
  (setq org-log-done "note")
  (setq org-hide-leading-stars t)
  (setq org-confirm-babel-evaluate nil)
  (setq org-directory (expand-file-name "~/Dropbox/org"))
  (setq org-default-notes-file
        (concat org-directory "/inbox.org"))
  (setq org-image-actual-width nil
        org-format-latex-options (plist-put org-format-latex-options :scale 1.5))
#+END_SRC

Set global keys for org mode to access agenda.

#+BEGIN_SRC emacs-lisp
  (global-set-key "\C-cl" 'org-store-link)
  (global-set-key "\C-ca" 'org-agenda)
  (global-set-key "\C-cc" 'org-capture)
  (global-set-key "\C-cb" 'org-iswitchb)
  (define-key global-map "\C-cc" 'org-capture)
#+END_SRC

Set up ob for executing code blocks

#+BEGIN_SRC emacs-lisp
  (use-package ob
    :ensure nil
    :config
    (org-babel-do-load-languages
     'org-babel-load-languages
     '((emacs-lisp . t)
       (js         . t)
       (java       . t)
       (haskell    . t)
       (python     . t)
       (ruby       . t)
       (org        . t)
       (matlab     . t)
       (ditaa      . t)
       (clojure    . t)
       (dot        . t))))
#+END_SRC

Exporting to html needs htmlize.

#+BEGIN_SRC emacs-lisp
  (use-package htmlize
    :commands (htmlize-file
               htmlize-buffer
               htmlize-region
               htmlize-many-files
               htmlize-many-files-dired
               htmlize-region-save-screenshot))
#+END_SRC

Add md backend

#+BEGIN_SRC emacs-lisp
  (require 'ox-md)
#+END_SRC

*** Templates
#+BEGIN_SRC emacs-lisp
  (setq org-capture-templates
        '(("t" "todo" entry (file+headline "~/Dropbox/org/inbox.org" "Tasks")
           "* TODO %?\n\n%i\n%a\n\n")))
#+END_SRC

*** Agenda

#+BEGIN_SRC emacs-lisp
  (setq org-agenda-files (mapcar 'expand-file-name
                                 '("~/Dropbox/org/inbox.org"
                                   "~/Dropbox/org/main.org"
                                   "~/Dropbox/org/tickler.org"))
        org-refile-targets '(("~/Dropbox/org/main.org" :maxlevel . 2)
                             ("~/Dropbox/org/someday.org" :level . 1)
                             ("~/Dropbox/org/tickler.org" :maxlevel . 2))
        org-todo-keywords '((sequence "TODO(t)" "WAITING(w)" "|" "DONE(d)" "CANCELLED(c)")))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (setq org-agenda-custom-commands 
        '(("w" "At work" tags-todo "@work"
           ((org-agenda-overriding-header "Work")))
          ("h" "At home" tags-todo "@home"
           ((org-agenda-overriding-header "Home")))
          ("u" "At uni" tags-todo "@uni"
           ((org-agenda-overriding-header "University")))))
#+END_SRC

*** Remove Binding
#+BEGIN_SRC emacs-lisp
  (define-key org-mode-map (kbd "C-,") nil)
#+END_SRC

*** Registers

#+BEGIN_SRC emacs-lisp
  (set-register ?l (cons 'file "~/.emacs.d/loader.org"))
  (set-register ?m (cons 'file "~/Dropbox/org/main.org"))
  (set-register ?i (cons 'file "~/Dropbox/org/inbox.org"))
#+END_SRC

** PDF Tools
#+BEGIN_SRC emacs-lisp
  (use-package pdf-tools
    :hook (doc-view-mode . pdf-view-mode)
    :commands pdf-view-mode)
#+END_SRC

* Programming
My emacs configuration is mostly focused on programming, therefore there is a
lot of different language support.

** Version Control and Project Management
*** Magit
#+BEGIN_SRC emacs-lisp
  (use-package magit
    :bind (("C-x g" . magit-status))
    :config
    (setq server-switch-hook nil))
#+END_SRC

#+RESULTS:
: magit-status

*** Projectile
#+BEGIN_SRC emacs-lisp
  (use-package projectile
    :config
    (projectile-mode +1)
    (define-key projectile-mode-map (kbd "C-c p") 'projectile-command-map)
    (setq projectile-enable-caching nil)
    (setq projectile-git-submodule-command "")
    (setq projectile-mode-line '(:eval (format " Proj[%s]" (projectile-project-name)))))

  (use-package counsel-projectile
    :config
    (counsel-projectile-mode t))
#+END_SRC

** Language Support
*** C++

Setting up CC mode with a hook that uses my settings.

#+BEGIN_SRC emacs-lisp
  (use-package cc-mode
    :config
    (add-to-list 'auto-mode-alist '("\\.h\\'" . c++-mode))
    (setq c-default-style "linux"
          tab-width 4
          c-indent-level 4)
    (defun my-c++-mode-hook ()
      (c-set-offset 'inline-open 0)
      (c-set-offset 'inline-close 0)
      (c-set-offset 'innamespace 0)
      (c-set-offset 'arglist-cont-nonempty 8)
      (setq indent-tabs-mode nil))
    (add-hook 'c-mode-hook 'my-c++-mode-hook)
    (add-hook 'c++-mode-hook 'my-c++-mode-hook)

    (define-key c-mode-map (kbd "C-c C-c") 'comment-or-uncomment-region))
#+END_SRC

Adding C headers to company backend for completion.

#+BEGIN_SRC emacs-lisp
  (use-package irony
    :config
    (add-hook 'c++-mode-hook 'irony-mode)
    (add-hook 'c-mode-hook 'irony-mode)
    (add-hook 'objc-mode-hook 'irony-mode)

    (defun my-irony-mode-hook ()
      (define-key irony-mode-map [remap completion-at-point]
        'irony-completion-at-point-async)
      (define-key irony-mode-map [remap complete-symbol]
        'irony-completion-at-point-async))
    (add-hook 'irony-mode-hook 'my-irony-mode-hook)
    (add-hook 'irony-mode-hook 'irony-cdb-autosetup-compile-options))

  (use-package company-irony)

  (use-package flycheck-irony
    :config
    (add-hook 'c++-mode-hook #'flycheck-irony-setup))

  (use-package company-c-headers
    :config
    (add-to-list 'company-backends 'company-c-headers)
    (add-to-list 'company-backends 'company-irony)

    (add-hook 'irony-mode-hook 'company-irony-setup-begin-commands))
#+END_SRC

Using clang format to format the region that is currently being selected (need
to install clang format script).

#+BEGIN_SRC emacs-lisp
  (use-package clang-format
    :bind (("C-c i" . 'clang-format-region)
           ("C-c u" . 'clang-format-buffer)))
#+END_SRC

*** Clojure
Using Cider for clojure environment.

#+BEGIN_SRC emacs-lisp
  (use-package cider
    :commands cider-mode
    :config
    (setq cider-repl-display-help-banner nil))
#+END_SRC

Adding hook to clojure mode to enable strict parentheses mode.

#+BEGIN_SRC emacs-lisp
  (add-hook 'clojure-mode-hook 'turn-on-smartparens-strict-mode)
#+END_SRC

*** CMake
#+BEGIN_SRC emacs-lisp
  (use-package cmake-mode
    :commands cmake-mode
    :config
    (setq auto-mode-alist
          (append
           '(("CMakeLists\\.txt\\'" . cmake-mode))
           '(("\\.cmake\\'" . cmake-mode))
           auto-mode-alist))
    (autoload 'cmake-mode "~/CMake/Auxiliary/cmake-mode.el" t))
#+END_SRC

*** Elm

#+BEGIN_SRC emacs-lisp
  (use-package elm-mode
    :mode ("\\.elm\\'"))
#+END_SRC

*** Emacs Lisp
Adding strict parentheses to emacs lisp.

#+BEGIN_SRC emacs-lisp
  (add-hook 'emacs-lisp-mode-hook 'turn-on-smartparens-strict-mode)
#+END_SRC

*** F#
F# mode for uni work.

#+BEGIN_SRC emacs-lisp
  (use-package fsharp-mode
    :commands fsharp-mode
    :config
    (defun y/fsharp-reload-file ()
      "Reloads the whole file when in fsharp mode."
      (interactive)
      (fsharp-eval-region (point-min) (point-max)))
      (add-hook 'fsharp-mode-hook
              (lambda () (local-set-key (kbd "C-c C-c") #'y/fsharp-reload-file))))
#+END_SRC

*** Haskell

Haskell mode with company mode completion.

#+BEGIN_SRC emacs-lisp
  (use-package haskell-mode
    :commands haskell-mode)

  (use-package interactive-haskell-mode
    :ensure haskell-mode
    :hook haskell-mode
    :init
    (custom-set-variables
     '(haskell-process-path-stack (expand-file-name "~/.local/bin/stack"))
     '(haskell-process-log t)))
#+END_SRC

*** Python
Elpy package for python, which provides an IDE type environment for python.

#+BEGIN_SRC emacs-lisp
  (use-package elpy
    :commands python-mode
    :config
    (elpy-enable)
    (setq py-python-command "python3")
    (setq python-shell-interpreter "python3"))

  (with-eval-after-load 'python
    (defun python-shell-completion-native-try ()
      "Return non-nil if can trigger native completion."
      (let ((python-shell-completion-native-enable t)
            (python-shell-completion-native-output-timeout
             python-shell-completion-native-try-output-timeout))
        (python-shell-completion-native-get-completions
         (get-buffer-process (current-buffer))
         nil "_"))))
#+END_SRC

*** JSON
JSON files should be opened in js-mode.
#+BEGIN_SRC emacs-lisp
  (add-to-list 'auto-mode-alist '("\\.json\\'" . js-mode))
#+END_SRC

*** SCSS
#+BEGIN_SRC emacs-lisp
  (use-package css-mode
    :ensure nil
    :commands (scss-mode
               css-mode)
    :config
    (setq css-indent-offset 2))
#+END_SRC

*** Shell
#+BEGIN_SRC emacs-lisp
  (setq sh-basic-offset 2)
  (setq sh-indentation 2)
#+END_SRC

** Completion Support
*** Company
#+BEGIN_SRC emacs-lisp
  (use-package company
    :config
    (add-hook 'after-init-hook 'global-company-mode)
    (setq company-backends (delete 'company-semantic company-backends))

    (define-key c-mode-map (kbd "C-c n") 'company-complete)
    (define-key c++-mode-map (kbd "C-c n") 'company-complete)
    (setq company-dabbrev-downcase 0))
#+END_SRC

*** Flycheck
Enabling global flycheck support.
#+BEGIN_SRC emacs-lisp
  (use-package flycheck
    :config (global-flycheck-mode))
#+END_SRC

*** Yasnippets
#+BEGIN_SRC emacs-lisp
  (use-package yasnippet
    :hook ((org-mode cc-mode) . yas-minor-mode)
    :config
    (yas-minor-mode 1))
#+END_SRC

* Look and Feel

Keybindings

#+BEGIN_SRC emacs-lisp
  (defun y/set-theme (theme)
    (load-theme theme t)
    (toggle-scroll-bar -1))

  (defun y/sanityinc-tomorrow-bright ()
    (interactive)
    (y/set-theme 'sanityinc-tomorrow-bright))

  (defun y/zenburn ()
    (interactive)
    (y/set-theme 'zenburn))

  (defun y/solarized-light ()
    (interactive)
    (y/set-theme 'solarized-light))

  (defun y/gruvbox ()
    (interactive)
    (y/set-theme 'gruvbox))

  (defun y/sanityinc-tomorrow-bright ()
    (interactive)
    (y/set-theme 'sanityinc-tomorrow-bright))

  (define-key y-map (kbd "1") 'y/sanityinc-tomorrow-bright)
  (define-key y-map (kbd "2") 'y/zenburn)
  (define-key y-map (kbd "3") 'y/solarized-light)
  (define-key y-map (kbd "4") 'y/gruvbox)
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (defadvice load-theme
      (before theme-dont-propagate activate)
    (mapc #'disable-theme custom-enabled-themes))

  (use-package solarized-theme
    :config
    (setq solarized-use-variable-pitch nil))

  (if (daemonp)
      (add-hook 'after-make-frame-functions
                (lambda (frame)
                  (select-frame frame)
                  (load-theme 'solarized-light t)
                  (toggle-scroll-bar -1)))
    (progn (load-theme 'solarized-light t)
           (toggle-scroll-bar -1)))
#+END_SRC

* Conclusion
Setting the gc-cons threshold back to what it was at the beginning.

#+BEGIN_SRC emacs-lisp
  (setq gc-cons-threshold 10000000)
#+END_SRC
